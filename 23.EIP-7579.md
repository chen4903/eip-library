# EIP-7579

- ERC-7579有一个专门的[网站](https://erc7579.com/)介绍它，估计是官网维护的

- EIP: https://eips.ethereum.org/EIPS/eip-7579
- 简介：ERC-7579 提出了一个最小化的、模块化的智能账户标准，以增强不同实现之间的互操作性。它定义了smart account和模块的基本接口，重点关注Execute、配置和callback，同时遵循 ERC-165、ERC-1271 和 ERC-4337 等标准。该提案旨在确保跨多种钱包应用程序和 SDK 的兼容性。
- ERC-7579是ERC-4337的超集，Safe7579 Adapter确保完全符合ERC-4337

## 一些概念

- smart account: 一个智能合约，它可以拥有多个模块，随时装载和拆卸
- 模块
  - Validator：用于在ERC-4337的验证
  - Executor：通过callback代表smart account执行交易
  - Fallback Handler：可以扩展smart account的fallback功能
- EntryPoint：根据ERC-4337规范，一个可信的单例合约。
- Validation：用于确定是否应在账户上执行的功能。在使用ERC-4337时，这个函数将是`validateUserOp()`。
- 用于确定是否应在账户上执行的功能。在使用ERC-4337时，这个函数将是validateUserOp。
- Execution：用于从user account执行操作。在使用ERC-4337时，这将由EntryPoint使用`userOp.callData()`来调用。

## 接口

必须实现的：

```solidity
interface IERC7579Execution {
    /**
     * @dev Executes a transaction on behalf of the account. MAY be payable.
     * @param mode The encoded execution mode of the transaction.
     * @param executionCalldata The encoded execution call data.
     *
     * MUST ensure adequate authorization control: e.g. onlyEntryPointOrSelf if used with ERC-4337
     * If a mode is requested that is not supported by the Account, it MUST revert
     */
    function execute(bytes32 mode, bytes calldata executionCalldata) external;

    /**
     * @dev Executes a transaction on behalf of the account. MAY be payable.
     *         This function is intended to be called by Executor Modules
     * @param mode The encoded execution mode of the transaction.
     * @param executionCalldata The encoded execution call data.
     *
     * @return returnData An array with the returned data of each executed subcall
     *
     * MUST ensure adequate authorization control: i.e. onlyExecutorModule
     * If a mode is requested that is not supported by the Account, it MUST revert
     */
    function executeFromExecutor(bytes32 mode, bytes calldata executionCalldata)
        external
        returns (bytes[] memory returnData);
}
```

账户还可以根据ERC-4337实现以下接口：

```solidity
/**
 * @dev ERC-4337 executeUserOp according to ERC-4337 v0.7
 *         This function is intended to be called by ERC-4337 EntryPoint.sol
 * @param userOp PackedUserOperation struct (see ERC-4337 v0.7+)
 * @param userOpHash The hash of the PackedUserOperation struct
 *
 * MUST ensure adequate authorization control: i.e. onlyEntryPoint
 */
function executeUserOp(PackedUserOperation calldata userOp, bytes32 userOpHash) external;
```

需要注意的是：如果一个用户执行`executeUserOp()`，这个方法应该确保账户排除userOp.calldata的4个最高有效字节，然后才执行。这些字节根据ERC-4337为`executeUserOp.selector`保留。因此，`userOp.callData[4:]`应该表示对用户的有效调用的调用数据。建议账户执行一个delegatecall以保留原始msg.sender到账户。

```solidity
(bool success, bytes memory innerCallRet) = address(this).delegatecall(userOp.callData[4:]);
```

账户配置接口：

```solidity
interface IERC7579AccountConfig {
    /**
     * @dev Returns the account id of the smart account
     * @return accountImplementationId the account id of the smart account
     *
     * MUST return a non-empty string
     * The accountId SHOULD be structured like so:
     *        "vendorname.accountname.semver"
     * The id SHOULD be unique across all smart accounts
     */
    function accountId() external view returns (string memory accountImplementationId);

    /**
     * @dev Function to check if the account supports a certain execution mode (see above)
     * @param encodedMode the encoded mode
     *
     * MUST return true if the account supports the mode and false otherwise
     */
    function supportsExecutionMode(bytes32 encodedMode) external view returns (bool);

    /**
     * @dev Function to check if the account supports a certain module typeId
     * @param moduleTypeId the module type ID according to the ERC-7579 spec
     *
     * MUST return true if the account supports the module type and false otherwise
     */
    function supportsModule(uint256 moduleTypeId) external view returns (bool);
}
```

smart account必须实现下面的接口：

```solidity
interface IERC7579ModuleConfig {
    event ModuleInstalled(uint256 moduleTypeId, address module);
    event ModuleUninstalled(uint256 moduleTypeId, address module);

    /**
     * @dev Installs a Module of a certain type on the smart account
     * @param moduleTypeId the module type ID according to the ERC-7579 spec
     * @param module the module address
     * @param initData arbitrary data that may be required on the module during `onInstall`
     * initialization.
     *
     * MUST implement authorization control
     * MUST call `onInstall` on the module with the `initData` parameter if provided
     * MUST emit ModuleInstalled event
     * MUST revert if the module is already installed or the initialization on the module failed
     */
    function installModule(uint256 moduleTypeId, address module, bytes calldata initData) external;

    /**
     * @dev Uninstalls a Module of a certain type on the smart account
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     * @param module the module address
     * @param deInitData arbitrary data that may be required on the module during `onInstall`
     * initialization.
     *
     * MUST implement authorization control
     * MUST call `onUninstall` on the module with the `deInitData` parameter if provided
     * MUST emit ModuleUninstalled event
     * MUST revert if the module is not installed or the deInitialization on the module failed
     */
    function uninstallModule(uint256 moduleTypeId, address module, bytes calldata deInitData) external;

    /**
     * @dev Returns whether a module is installed on the smart account
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     * @param module the module address
     * @param additionalContext arbitrary data that may be required to determine if the module is installed
     *
     * MUST return true if the module is installed and false otherwise
     */
    function isModuleInstalled(uint256 moduleTypeId, address module, bytes calldata additionalContext) external view returns (bool);
}
```

## 执行模式

execution mode是一个bytes32值，其结构如下：

- callType：0x00表示单个调用，0x01表示批量调用，0xfe表示静态调用，0xff表示delegate调用

- execType：0x00表示失败时回滚执行，0x01表示失败时不进行回滚但实现某种错误处理

- unused：这个范围保留用于未来的标准化

- modeSelector：一个额外的模式选择器，可用于创建进一步的执行模式

- modePayload：要传递的额外数据

| CallType | ExecType | Unused  | ModeSelector | ModePayload |
| -------- | -------- | ------- | ------------ | ----------- |
| 1 byte   | 1 byte   | 4 bytes | 4 bytes      | 22 bytes    |

## Hooks

Hooks是这个标准的可选扩展。smart account可以使用Hooks在执行单个或批量执行之前和/之后执行自定义逻辑和检查。为了符合这个可选扩展，smart account：

- 必须在通过`execute()`或`executeFromExecutor()`进行任何调用或调用批次之前调用一个或多个钩子的`preCheck()`函数
- 必须在通过`execute()`或`executeFromExecutor()`进行任何调用或调用批次之后调用一个或多个钩子的`postCheck()`函数
- 建议在执行`installModule()`或`uninstallModule()`调用之前和之后调用`preCheck()`和`postCheck()`
- 建议在通过其他（自定义）函数执行调用之前和之后调用`preCheck()`和`postCheck()`。

## Fallbacks

smart account可以实现一个Fallback函数，将调用转发给Fallback Handler。如果smart account装载了Fallback Handler，它：

- 必须使用call或staticcall来调用Fallback Handler

- 必须使用ERC-2771将原始msg.sender添加到发送给Fallback Handler的calldata中

- 必须根据calldata中的函数选择器路由到Fallback Handler

- 可以实现授权控制，这应该通过Hooks来完成

如果账户通过Fallback添加功能，这些应该被视为与账户原生实现这些功能相同。

## Modules

本标准将Modules分为以下不同类型，每种类型都有一个唯一且递增的标识符，账户、模块和其他实体必须使用这些标识符来识别模块类型：

- Validation（类型标识符：1）

- Execution（类型标识符：2）

- Fallback（类型标识符：3）

- Hooks（类型标识符：4）

> 注意：单个模块可以是多种类型。

模块必须实现以下接口：

```solidity
interface IERC7579Module {
     /**
     * @dev This function is called by the smart account during installation of the module
     * @param data arbitrary data that may be required on the module during `onInstall` initialization
     *
     * MUST revert on error (e.g. if module is already enabled)
     */
    function onInstall(bytes calldata data) external;

    /**
     * @dev This function is called by the smart account during uninstallation of the module
     * @param data arbitrary data that may be required on the module during `onUninstall` de-initialization
     *
     * MUST revert on error
     */
    function onUninstall(bytes calldata data) external;

    /**
     * @dev Returns boolean value if module is a certain type
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     *
     * MUST return true if the module is of the given type and false otherwise
     */
    function isModuleType(uint256 moduleTypeId) external view returns(bool);
}
```

### Validators

必须实现下面的接口：

```solidity
interface IERC7579Validator is IERC7579Module {
    /**
     * @dev Validates a UserOperation
     * @param userOp the ERC-4337 PackedUserOperation
     * @param userOpHash the hash of the ERC-4337 PackedUserOperation
     *
     * MUST validate that the signature is a valid signature of the userOpHash
     * SHOULD return ERC-4337's SIG_VALIDATION_FAILED (and not revert) on signature mismatch
     */
    function validateUserOp(PackedUserOperation calldata userOp, bytes32 userOpHash) external returns (uint256);

    /**
     * @dev Validates a signature using ERC-1271
     * @param sender the address that sent the ERC-1271 request to the smart account
     * @param hash the hash of the ERC-1271 request
     * @param signature the signature of the ERC-1271 request
     *
     * MUST return the ERC-1271 `MAGIC_VALUE` if the signature is valid
     * MUST NOT modify state
     */
    function isValidSignatureWithSender(address sender, bytes32 hash, bytes calldata signature) external view returns (bytes4);
}
```

### Executors

Executors必须实现IERC7579Module接口，并具有模块类型标识：2。

### Fallback Handlers

Fallback Handlers必须实现IERC7579Module接口，并具有模块类型ID：3。

### Hooks

必须实现：

```solidity
interface IERC7579Hook is IERC7579Module {
    /**
     * @dev Called by the smart account before execution
     * @param msgSender the address that called the smart account
     * @param value the value that was sent to the smart account
     * @param msgData the data that was sent to the smart account
     *
     * MAY return arbitrary data in the `hookData` return value
     */
    function preCheck(address msgSender, uint256 value, bytes calldata msgData) external returns (bytes memory hookData);

    /**
     * @dev Called by the smart account after execution
     * @param hookData the data that was returned by the `preCheck` function
     *
     * MAY validate the `hookData` to validate transaction context of the `preCheck` function
     */
    function postCheck(bytes calldata hookData) external;
}
```





































