# EIP-3009

## 简介

一种新的代币标准。

通过EIP-712的签名规范来实现ERC20的授权交易，好处：

- 将gas委托给其他人，
- 用代币支付gas，而不是 ETH，
- 在单个tx中执行一个或多个token交易和其他操作，
- 将 ERC-20 代币转移到另一个地址，并让接收者提交交易，
- 以最小的开销批处理多个tx
- 创建并执行多个tx，而不必担心它们由于意外的随机数重用或矿工的不当排序而失败。

## 动机

EIP-3009与EIP-2612的区别：

- EIP-2612 使用顺序随机数，但EIP-3009使用随机 32 字节随机数
- EIP-2612依赖于ERC20的`approve-transferFrom`模式

使用顺序随机数的问题：它不允许用户一次执行多个交易而不冒交易失败的风险，也就是说如果两笔交易在同一个块中打包，那么矿工就必须按照顺序打包否则就会失败

- Dapp可能无意中重用区块链中尚未处理的随机数
- 矿工可能会以错误的顺序处理交易。

如果天然气价格非常高并且交易经常排队并且长时间未得到确认，这可能会特别成问题。非顺序随机数允许用户同时创建任意数量的交易。

ERC20的`approve-transferFrom`模式很容易受到攻击，因此EIP-3009使用一种新的模式。

## 代码

### event

```solidity
event AuthorizationUsed(
    address indexed authorizer,
    bytes32 indexed nonce
);

// keccak256("TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
bytes32 public constant TRANSFER_WITH_AUTHORIZATION_TYPEHASH = 0x7c7c6cdb67a18743f49ec6fa9b35f50d52ed05cbed4cc592e13b44501c1a2267;

// keccak256("ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
bytes32 public constant RECEIVE_WITH_AUTHORIZATION_TYPEHASH = 0xd099cc98ef71107a616c4f0f941f04c322d8e254fe26b3c6668db87aae413de8;

/**
 * @notice Returns the state of an authorization
 * @dev Nonces are randomly generated 32-byte data unique to the authorizer's
 * address
 * @param authorizer    Authorizer's address
 * @param nonce         Nonce of the authorization
 * @return True if the nonce is used
 */
function authorizationState(
    address authorizer,
    bytes32 nonce
) external view returns (bool);

/**
 * @notice Execute a transfer with a signed authorization
 * @param from          Payer's address (Authorizer)
 * @param to            Payee's address
 * @param value         Amount to be transferred
 * @param validAfter    The time after which this is valid (unix time)
 * @param validBefore   The time before which this is valid (unix time)
 * @param nonce         Unique nonce
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function transferWithAuthorization(
    address from,
    address to,
    uint256 value,
    uint256 validAfter,
    uint256 validBefore,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;

/**
 * @notice Receive a transfer with a signed authorization from the payer
 * @dev This has an additional check to ensure that the payee's address matches
 * the caller of this function to prevent front-running attacks. (See security
 * considerations)
 * @param from          Payer's address (Authorizer)
 * @param to            Payee's address
 * @param value         Amount to be transferred
 * @param validAfter    The time after which this is valid (unix time)
 * @param validBefore   The time before which this is valid (unix time)
 * @param nonce         Unique nonce
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function receiveWithAuthorization(
    address from,
    address to,
    uint256 value,
    uint256 validAfter,
    uint256 validBefore,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;
```

### 可选的

```solidity
event AuthorizationCanceled(
    address indexed authorizer,
    bytes32 indexed nonce
);

// keccak256("CancelAuthorization(address authorizer,bytes32 nonce)")
bytes32 public constant CANCEL_AUTHORIZATION_TYPEHASH = 0x158b0a9edf7a828aad02f63cd515c68ef2f50ba807396f6d12842833a1597429;

/**
 * @notice Attempt to cancel an authorization
 * @param authorizer    Authorizer's address
 * @param nonce         Nonce of the authorization
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function cancelAuthorization(
    address authorizer,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;
```

例子

```solidity
DomainSeparator := Keccak256(ABIEncode(
  Keccak256(
    "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
  ),
  Keccak256("USD Coin"),                      // name
  Keccak256("2"),                             // version
  1,                                          // chainId
  0xa0b86991c6218b36c1d19d4a2e9eb0ce3606eb48  // verifyingContract
))
```

通过上面一堆东西，得到一个hash，然后可以用私钥对这个hash进行签名

例子

```
// Transfer With Authorization
TypeHash := Keccak256(
  "TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)"
)
Params := { From, To, Value, ValidAfter, ValidBefore, Nonce }

// ReceiveWithAuthorization
TypeHash := Keccak256(
  "ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)"
)
Params := { From, To, Value, ValidAfter, ValidBefore, Nonce }

// CancelAuthorization
TypeHash := Keccak256(
  "CancelAuthorization(address authorizer,bytes32 nonce)"
)
Params := { Authorizer, Nonce }
```

```
// "‖" denotes concatenation.
Digest := Keecak256(
  0x1901 ‖ DomainSeparator ‖ Keccak256(ABIEncode(TypeHash, Params...))
)

{ v, r, s } := Sign(Digest, PrivateKey)
```

看起来有很多个参数，我们可以在函数的参数中不搞这么多参数，使用bytes就行，然后在函数体里面解析这个bytes

```solidity
// keccak256("receiveWithAuthorization(address,address,uint256,uint256,uint256,bytes32,uint8,bytes32,bytes32)")[0:4]
bytes4 private constant _RECEIVE_WITH_AUTHORIZATION_SELECTOR = 0xef55bec6;

function deposit(address token, bytes calldata receiveAuthorization)
    external
    nonReentrant
{
    (address from, address to, uint256 amount) = abi.decode(
        receiveAuthorization[0:96],
        (address, address, uint256)
    );
    require(to == address(this), "Recipient is not this contract");

    (bool success, ) = token.call(
        abi.encodePacked(
            _RECEIVE_WITH_AUTHORIZATION_SELECTOR,
            receiveAuthorization
        )
    );
    require(success, "Failed to transfer tokens");

    ...
}
```

### 节点提供商

授权签名可以通过 web3 提供者的`eth_signTypedData{_v4}`方法获得。

```js
const data = {
  types: {
    EIP712Domain: [
      { name: "name", type: "string" },
      { name: "version", type: "string" },
      { name: "chainId", type: "uint256" },
      { name: "verifyingContract", type: "address" },
    ],
    TransferWithAuthorization: [
      { name: "from", type: "address" },
      { name: "to", type: "address" },
      { name: "value", type: "uint256" },
      { name: "validAfter", type: "uint256" },
      { name: "validBefore", type: "uint256" },
      { name: "nonce", type: "bytes32" },
    ],
  },
  domain: {
    name: tokenName,
    version: tokenVersion,
    chainId: selectedChainId,
    verifyingContract: tokenAddress,
  },
  primaryType: "TransferWithAuthorization",
  message: {
    from: userAddress,
    to: recipientAddress,
    value: amountBN.toString(10),
    validAfter: 0,
    validBefore: Math.floor(Date.now() / 1000) + 3600, // Valid for an hour
    nonce: Web3.utils.randomHex(32),
  },
};

const signature = await ethereum.request({
  method: "eth_signTypedData_v4",
  params: [userAddress, JSON.stringify(data)],
});

const v = "0x" + signature.slice(130, 132);
const r = signature.slice(0, 66);
const s = "0x" + signature.slice(66, 130);
```

## 原理

### 随机数

- 对于原生以太坊交易，当将随机数值过高的交易提交到网络时，它将保持待处理状态，直到确认消耗较低未使用随机数的交易。
- 然而，对于元交易，当提交包含过高的顺序随机数值的交易时，它将立即恢复并失败，而不是保持待处理状态，从而导致天然气浪费。

> 这个意思是：假设当前的nonce是10
> 如果只是发送原生的ETH，如果nonce设置为20，则交易会pending，
> 如果是调用函数的交易，如果nonce设置为20，则交易会revert

- 事实上，矿工还可以对交易重新排序，并按照他们想要的顺序将它们包含在区块中，这也使得元交易有可能失败，即使随机数使用的是正确的。（例如，用户提交随机数 3、4 和 5，但矿工最终将它们作为 4、5、3 包含在区块中，导致只有 3 个成功）
- 最后，当同时使用不同的App时，在没有某种链外随机数跟踪器的情况下，如果存在已使用但尚未提交和确认的随机数，则无法确定正确的下一个随机数值是什么通过网络。
- 在高 Gas 价格的情况下，交易往往会长时间“卡在”内存池中。在这种情况下，同一个随机数更有可能被无意中重复使用两次。例如，如果您进行一项使用一个应用程序中的顺序随机数的元交易，并在前一个应用程序确认之前切换到另一个应用程序进行另一笔元交易，则如果该应用程序纯粹依赖于数据，则将使用相同的随机数链上可用，导致其中一项交易失败。
- 总之，保证交易顺序的唯一方法是中继者一次提交一个交易，在每次提交之间等待确认（并且提交顺序可以是某些链下元数据的一部分），呈现顺序随机数无关。

依赖中继者为您提交交易意味着您可能无法准确控制交易提交的时间。

这些参数允许用户将交易安排为仅在未来或特定截止日期之前有效，从而保护用户免受因提交太晚或太早而可能导致的潜在不良影响。

### EIP712

- EIP-712 确保生成的签名仅对代币合约的该特定实例有效，并且不能在具有不同链 ID 的不同网络上重放。
- 这是通过将合约地址和链 ID 合并到称为域分隔符的 Keccak-256 哈希摘要中来实现的。用于派生域分隔符的实际参数集取决于实施合同，但强烈建议包含字段`verifyingContract`和`chainId`。

### 向后兼容

新合约受益于能够直接利用 EIP-3009 来创建原子交易，但现有合约可能仍依赖于传统的 ERC-20 配额模式 ( `approve`/ `transferFrom`)。

为了向使用 ERC-20 配额模式的现有合约（“parent合约”）添加对 EIP-3009 的支持，可以构建一个转发合约（“`转发器`”），它需要授权并执行以下操作：

1. 从authorization中提取用户和deposit的数量
2. 调用`receiveWithAuthorization`将钱从用户转移到`转发器`
3. 授权parent合约使用forwarder的钱
4. 调用parent合约上消耗allowance的方法
5. 将任何生成的代币的所有权转移回用户

例子：我在ERC3009合约中拥有50元，我想将这50元转换为某个ERC20代币，那么我这么做：调用`转发器`的`deposit()`，他会将我的50元从ERC3009转到`转发器`中，然后转发器调用ERC20的`deposit()`，拥有了50元的ERC20代币，并将这50元给了我，这样我就拥有了50元的ERC20代币。（这个ERC20代币是parent合约，它deposit的时候是用另外一个ERC20代币进行授权）

```solidity
interface IDeFiToken {
    function deposit(uint256 amount) external returns (uint256);

    function transfer(address account, uint256 amount)
        external
        returns (bool);
}

contract DepositForwarder {
    bytes4 private constant _RECEIVE_WITH_AUTHORIZATION_SELECTOR = 0xef55bec6;

    IDeFiToken private _parent;
    IERC20 private _token;

    constructor(IDeFiToken parent, IERC20 token) public {
        _parent = parent;
        _token = token;
    }

    function deposit(bytes calldata receiveAuthorization)
        external
        nonReentrant
        returns (uint256)
    {
        (address from, address to, uint256 amount) = abi.decode(
            receiveAuthorization[0:96],
            (address, address, uint256)
        );
        require(to == address(this), "Recipient is not this contract");

        (bool success, ) = address(_token).call(
            abi.encodePacked(
                _RECEIVE_WITH_AUTHORIZATION_SELECTOR,
                receiveAuthorization
            )
        );
        require(success, "Failed to transfer to the forwarder");

        require(
            _token.approve(address(_parent), amount),
            "Failed to set the allowance"
        );

        uint256 tokensMinted = _parent.deposit(amount);
        require(
            _parent.transfer(from, tokensMinted),
            "Failed to transfer the minted tokens"
        );

        uint256 remainder = _token.balanceOf(address(this);
        if (remainder > 0) {
            require(
                _token.transfer(from, remainder),
                "Failed to refund the remainder"
            );
        }

        return tokensMinted;
    }
}
```

## 实现

EIP3009.sol

```solidity
abstract contract EIP3009 is IERC20Transfer, EIP712Domain {
    // keccak256("TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
    bytes32 public constant TRANSFER_WITH_AUTHORIZATION_TYPEHASH = 0x7c7c6cdb67a18743f49ec6fa9b35f50d52ed05cbed4cc592e13b44501c1a2267;

    // keccak256("ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
    bytes32 public constant RECEIVE_WITH_AUTHORIZATION_TYPEHASH = 0xd099cc98ef71107a616c4f0f941f04c322d8e254fe26b3c6668db87aae413de8;

    mapping(address => mapping(bytes32 => bool)) internal _authorizationStates;

    event AuthorizationUsed(address indexed authorizer, bytes32 indexed nonce);

    string internal constant _INVALID_SIGNATURE_ERROR = "EIP3009: invalid signature";

    function authorizationState(address authorizer, bytes32 nonce)
        external
        view
        returns (bool)
    {
        return _authorizationStates[authorizer][nonce];
    }

    function transferWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external {
        require(now > validAfter, "EIP3009: authorization is not yet valid");
        require(now < validBefore, "EIP3009: authorization is expired");
        require(
            !_authorizationStates[from][nonce],
            "EIP3009: authorization is used"
        );

        bytes memory data = abi.encode(
            TRANSFER_WITH_AUTHORIZATION_TYPEHASH,
            from,
            to,
            value,
            validAfter,
            validBefore,
            nonce
        );
        require(
            EIP712.recover(DOMAIN_SEPARATOR, v, r, s, data) == from,
            "EIP3009: invalid signature"
        );

        _authorizationStates[from][nonce] = true;
        emit AuthorizationUsed(from, nonce);

        _transfer(from, to, value);
    }
}
```

IERC20Transfer..sol

```solidity
abstract contract IERC20Transfer {
    function _transfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal virtual;
}
```

EIP712Domain.sol

```solidity
abstract contract EIP712Domain {
    bytes32 public DOMAIN_SEPARATOR;
}
```

EIP712.sol

```solidity
library EIP712 {
    // keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)")
    bytes32 public constant EIP712_DOMAIN_TYPEHASH = 0x8b73c3c69bb8fe3d512ecc4cf759cc79239f7b179b0ffacaa9a75d522b39400f;

    function makeDomainSeparator(string memory name, string memory version)
        internal
        view
        returns (bytes32)
    {
        uint256 chainId;
        assembly {
            chainId := chainid()
        }

        return
            keccak256(
                abi.encode(
                    EIP712_DOMAIN_TYPEHASH,
                    keccak256(bytes(name)),
                    keccak256(bytes(version)),
                    address(this),
                    bytes32(chainId)
                )
            );
    }

    function recover(
        bytes32 domainSeparator,
        uint8 v,
        bytes32 r,
        bytes32 s,
        bytes memory typeHashAndData
    ) internal pure returns (address) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                domainSeparator,
                keccak256(typeHashAndData)
            )
        );
        address recovered = ecrecover(digest, v, r, s);
        require(recovered != address(0), "EIP712: invalid signature");
        return recovered;
    }
}
```

可以在此[存储库](https://github.com/CoinbaseStablecoin/eip-3009/blob/master/contracts/lib/EIP3009.sol)中找到 EIP-3009 的完整工作实现。该存储库还包括使用上面提供的 EIP-712 库代码的[ EIP-2612 实现](https://github.com/CoinbaseStablecoin/eip-3009/blob/master/contracts/lib/EI32612.sol)。

## 安全

从其他智能合约调用时使用`receiveWithAuthorization`而不是`transferWithAuthorization`。攻击者有可能监视交易池以提取传输授权并在不调用包装器函数的情况下预先运行调用`transferWithAuthorization`以执行传输。这可能会导致存款未被处理、被锁定。`receiveWithAuthorization`通过执行额外的检查来确保调用者是收款人，可以防止这种情况发生。此外，如果有多个合约函数接受接收授权，应用程序开发人员可以将随机数的一些前导字节用作标识符，以防止交叉使用。

> 比如：某个合约A兼容了ERC3009，然后说要发送一笔钱到合约A，合约A到方法xxx()回会调用transferWithAuthorization, 然后修改状态。但是被抢跑了，有人帮你调用transferWithAuthorization了，则无法再调用xxx()，这就意味着合约A获得了这笔钱，但是你没有获得合约A中的相应资产

当同时提交多个传输时，请注意中继者和矿工将决定它们的处理顺序。如果交易彼此不依赖的话，这一般不会有问题，但对于彼此高度依赖的交易，建议一次提交一个签名授权。

使用`ecrecover`时必须拒绝零地址，以防止零地址资金未经授权的转账和审批。当提供格式错误的签名时，内置函数`ecrecover`会返回零地址。









